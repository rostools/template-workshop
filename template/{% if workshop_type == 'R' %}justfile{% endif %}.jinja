@_default:
  just --list --unsorted

# Run all recipes
run-all: install-dependencies check-spelling style build-website

# Install or update the pre-commit hooks
install-precommit:
  # Install pre-commit hooks
  uvx pre-commit install
  # Run pre-commit hooks on all files
  uvx pre-commit run --all-files
  # Update versions of pre-commit hooks
  uvx pre-commit autoupdate

# Install package dependencies
install-dependencies:
  #!/usr/bin/Rscript
  pak::pak(ask = FALSE)

# Check spelling
check-spelling:
  uvx typos

# Style all R code
style:
  # Need to install air first
  air format .

# Build Quarto website
build-website:
  quarto render
